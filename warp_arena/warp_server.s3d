#include <Script3d.h>
#include "warp_player.s3d"

#define SERVER_PORT 50023
#define CLIENT_PORT 50025

#define CONNECTION_REQUEST	(0)
#define CONNECTION_OK			(1)
#define POSITION_PDU			(2)
#define NEW_PLAYER				(3)
#define OLD_PLAYERS				(4)
#define PLAYER_EXIT				(5)
#define EXIT_NOTIFICATION		(6)
#define CHANGE_WEAPON			(7)

var net_channel;
var my_ip;
var players;
var players_ip;
var num_rows;
var num_columns;

var id;
var last_send;
var map;
var address1;

function OnDownload()
{
	// TODO: download your resources here
	// E.g.: FileDownload("resource.ext");
	FileDownload("XVRResources.zip");
}

function OnInit()
{
	net_channel = NetCreateChannel(SERVER_PORT, "0.0.0.0", VR_NO_BLOCKING);
	var address1 = "................";

	players = array(0);
	players_ip = array(0);
	id = 0;
	
	last_send = 0;
	
	my_ip = NetGetIP();
	
	SetTimeStep(1);
}

function player_spawn()
{
	var rand_row;
	var rand_col;
	var pdu;
	var pos;
	
	pdu = array(3);
	
	pdu[0] = CONNECTION_OK;
	
	rand_row = rand(9);
	rand_row += 12;
	rand_col = rand(9);
	rand_col += 18;
	pos = [rand_row, 0, rand_col];
	pdu[1] = pos;
	
	id += 1;
	
	pdu[2] = id;

	return pdu;
}

function mySend(address, port, data)
{
	NetVarSendTo(address, port, data);
}

function delete_player(exit_id)
{
	var exit_pdu = array(2);
	exit_pdu[0] = EXIT_NOTIFICATION;
	exit_pdu[1] = exit_id;
	foreach (var t in players_ip)
		mySend(t[1], CLIENT_PORT, exit_pdu);
}

function OnTimer()
{
	var curr_time = GetTime();
	var received_data = NetVarReceiveFrom(net_channel, &address1);
	if (received_data != null)
	{
		if (received_data[0] == CONNECTION_REQUEST)
		{
			var ack = player_spawn();
			mySend(address1, CLIENT_PORT, ack);
			trace(ack);

			var new_ip = array(2);
			new_ip[0] = ack[2];
			new_ip[1] = address1;
			aadd(players_ip, new_ip);	

			if (len(players) > 0)
			{
				var old_players_arr = array(2);
				old_players_arr[0] = OLD_PLAYERS;
				old_players_arr[1] = players;
				mySend(address1, CLIENT_PORT, old_players_arr);
			}
			
			//id, position, direction
			var player = array(4);
			player[0] = NEW_PLAYER;
			player[1] = ack[2];
			player[2] = ack[1];
			player[3] = 0;
			aadd(players, player);
			
			foreach (var t in players_ip)
				if (ack[2] != t[0])
					mySend(t[1], CLIENT_PORT, player);
		}
		else if (received_data[0] == POSITION_PDU)
		{
			foreach (var t in players_ip)
				if (received_data[3] != t[0])
					mySend(t[1], CLIENT_PORT, received_data);
		}
		else if (received_data[0] == CHANGE_WEAPON)
		{
			foreach (var t in players_ip)
				if (received_data[1] != t[0])
					mySend(t[1], CLIENT_PORT, received_data);
		}
		else if (received_data[0] == PLAYER_EXIT)
		{
			var exit_id = received_data[1];
			for (var t = 0; t < len(players); t++)
			{
				var g = players[t][1];
				if (players[t][1] == exit_id)
				{
					adel(players, t);
					adel(players_ip, t);
					delete_player(exit_id);
				}
			}
		}
	}
}

function OnFrame()
{
	
}

function OnEvent()
{
	
}

function OnError()
{
	
}

function OnExit()
{
	
}
function DownloadReady()
{
	
}