/******************************************************************************************************************
 *                                           |WARP_ARENA.S3D|                                                     *
 *----------------------------------------------------------------------------------------------------------------*
 * This is the arena file, and more in depth it is used to initialize the players arena, structures and acts as   *
 * client for his connection with the server. The OnInit initializes the lighting of the scene, then creates the  *
 * level by instantiating a warp_level class, and lastly asks the server for a game addition, then creating a     *
 * player with the information returned by the server.                                                            *
 * At each frame it then starts by setting the new position of the player, and then checking weather the new      *
 * position changed from the last one, and in positive case it sends a PDU to the server in order to notificate   *
 * the other players, and does the same with the angle and the weapon. It then draws the player, but firs it      *
 * checks weather he is in first person.                                                                          *
 * The OnTimer function is used to keep track of messages from the server, in order to change the position, the   *
 * weapon or other graphical effects of the players, as well as deleting him in case of need.                     *
 ******************************************************************************************************************/

/* Include s3d symbols */
#include <Script3d.h>
#include <Camera.s3d.h>
#include "warp_player.s3d"
#include "warp_level.s3d"
#include "warp_enemy.s3d"
#include "warp_supplies.s3d"
//client/server defines
#define SERVER_IP 		"192.168.1.31"
#define SERVER_PORT		50023
#define CLIENT_PORT		50025

/****************************************************
 *                     |PDUs|                       *
 *--------------------------------------------------*
 *                                                  *
 * CONNECTION_REQUEST: sent by a player that wants  *
 * to join a game.                                  *
 *                                                  *
 * CONNECTION_OK: sent by the serve once the request*
 * is accepted.                                     *
 *                                                  *
 * POSITION_PDU: sent by a player once it changes   *
 * his ange or his position. It is entirely sent to *
 * each other player in the game.                   *
 *                                                  *
 * NEW_PLAYER: sent by the server to all players to *
 * notificate a new player has entered the arena.   *
 *                                                  *
 * OLD_PLAYERS: once a player has entered a game,   *
 * the server informs him about the players that are*
 * already playing.                                 *
 *                                                  *
 * PLAYER_EXIT: used by a player to notificate he is*
 * quitting the game.                               *
 *                                                  *
 * EXIT_NOTIFICATION: used by the server to inform  *
 * all players that another one has left the game.  *
 *                                                  *
 * CHANGE_WEAPON: sent by a client once he changes  *
 * weapon. It is sent as it is from the server to   *
 * all clients.                                     *
 *                                                  *
 * NEW_AMMO: a new life or ammonition box has       *
 * entered the arena!                               *
 *                                                  *
 * COLLECTED_AMMO: notifies all players that the    *
 * ammonition box is no more available.             *
 *                                                  *
 * OLD_AMMO: passed to players when they enter the  *
 * arena. It is a vector containing all the         *
 * currently spawned boxes on the map.              *
 *                                                  *
 * SHOT_PDU: notifies that a player shot a bullet.  *
 *                                                  *
 * BULLET_ON_WALL: notifies all players that a      *
 * bullet has hit a wall, in order to delete it.    *
 ****************************************************/
#define CONNECTION_REQUEST		(0)
#define CONNECTION_OK			(1)
#define POSITION_PDU			(2)
#define NEW_PLAYER				(3)
#define OLD_PLAYERS				(4)
#define PLAYER_EXIT				(5)
#define EXIT_NOTIFICATION		(6)
#define CHANGE_WEAPON			(7)
#define NEW_AMMO				(8)
#define COLLECTED_AMMO			(9)
#define OLD_AMMO				(10)
#define SHOT_PDU				(11)
#define BULLET_ON_WALL			(12)
#define DEAD_PLAYER				(13)
#define BULLET_ON_PERSON		(14)
#define SERVER_BUSY				(15)
#define CHANGED_SCORE			(16)
#define NEW_SCORE				(17)
#define OLD_SCORES				(18)
#define KILL_SCORE				(19)

#define connection_retry 		500

/* Set global scene parameters */
SET SCENE_FOV  = 60;
SET SCENE_NEAR = 0.5;
SET SCENE_FAR  = 1000;

/* Light globals */
var Light0;
var PosL = [0, 16.0, 0];	// default light position
var Light1;
var PosL1 = [0, 16.0, 43];	// default light position
var Light2;
var PosL2 = [202, 16.0, 0];	// default light position
var Light3;
var PosL3 = [202, 16, 43];	// default light position

var mylevel;
var myenemy;
var myplayer;
var mysupply;
var mybox;
var arr_supplies;
var score_array;

var net_channel;
var players;
var old_pos; //old position to check
var position; //new position to check
var old_angle; //old direction to check
var angle; //new direction to check
var old_weapon; //used to check weather the weapon has changed
var enemy_bullets;
var window;
var welcome_fx;

var address1;
var arr;

/* Function declarations */
function CameraMoveMouse();

function mySend(address, port, data)
{
	NetVarSendTo(address, port, data);
}

function OnDownload()
{
	// TODO: download your resources here
	// E.g.: FileDownload("resource.ext");
	FileDownload("XVRResources.zip");
}

function OnInit(params)
{
	window = CVmWebBrowser();
	welcome_fx  = CVmVRAWAV("welcome.wav");
	obstacle_array = array(0);
	/* initialize camera */
	CameraGetCurrent().SetPosition(CamPos);

	/* initialize light */
	Light0 = CVmLight();
	Light0.SetPosition(PosL);
	Light0.SetDiffuse(1, 1, 1);
	Light0.Enable();	
	
	
	Light1 = CVmLight();
	Light1.SetPosition(PosL1);
	Light1.SetDiffuse(10, 10, 10);
	Light1.Enable();
	
	Light2 = CVmLight();
	Light2.SetPosition(PosL2);
	Light2.SetDiffuse(1, 1, 1);
	Light2.Enable();	
	
	
	Light3 = CVmLight();
	Light3.SetPosition(PosL3);
	Light3.SetDiffuse(10, 10, 10);
	Light3.Enable();

	// TODO: put your initialization code here
	mylevel = level();
	mylevel.init("level.txt");
	
	net_channel = NetCreateChannel(CLIENT_PORT, "0.0.0.0", VR_NO_BLOCKING);
	players = array(0);
	
	old_weapon = 0;
	enemy_bullets = array(0);
	
	mysupply = supplies();
	mysupply.init();
	
	var connection_pdu = array(1);
	connection_pdu[0] = CONNECTION_REQUEST;
	var rcv = false;
	
	address1 = "................";
	var lastsend = GetTime();
	var rcv_data;
	
	NetVarSendTo(SERVER_IP, SERVER_PORT, connection_pdu);
	
	do
	{
		if (GetTime() - lastsend > connection_retry)
		{
			NetVarSendTo(SERVER_IP, SERVER_PORT, connection_pdu);
			lastsend = GetTime();
		}
		rcv_data = NetVarReceiveFrom(net_channel, &address1);
		
		rcv = (rcv_data != null && rcv_data[0] == CONNECTION_OK) ?
			true : rcv;
		if (rcv_data != null && rcv_data[0] == SERVER_BUSY)
		{
			var y = "The game has started, retry later!";
			trace(y);
			window.Quit();
		}	
		
	} while (rcv == true);
	
	old_pos = rcv_data[1];
	position = rcv_data[1];
	old_angle = 0;
	angle = 0;
	
	arr_supplies = array(0);
	score_array = array(0);
	
	myplayer = player();
	myplayer.init([1, 1, 1], rcv_data[2]);
	myplayer.setposition(rcv_data[1]);
	welcome_fx.play();
	
	SetTimeStep(1);
}

function OnExit()
{
	var exit_pdu = array(2);
	exit_pdu[0] = PLAYER_EXIT;
	exit_pdu[1] = myplayer.id;
	mySend(SERVER_IP, SERVER_PORT, exit_pdu);
}

function OnFrame()
{ 
	SceneBegin();
	myplayer.shot_cnt = (myplayer.shot_cnt+1)%min_shot;
	if (myplayer.has_shot && (myplayer.shot_cnt == 0))
		myplayer.has_shot = false;
	/* manage camera */
	CameraMoveMouse();

	myplayer.update();
	position = myplayer.position;
	angle = myplayer.angle;
	
	for (var k = 0; k < len(myplayer.bullet_array); k++)
	{
		if (myplayer.bullet_array[k].hit_fg == true)
		{
			var wall_bullet = array(3);
			wall_bullet[0] = BULLET_ON_WALL;
			wall_bullet[1] = myplayer.id;
			wall_bullet[2] = myplayer.bullet_array[k].bull_id;
			mySend(SERVER_IP, SERVER_PORT, wall_bullet);
			adel(myplayer.bullet_array, k);
		}
	}
	
	if (myplayer.shot_flag == 1)
	{
		myplayer.shot_flag = 0;
		var bullet_pdu = array(3);
		bullet_pdu[0] = SHOT_PDU;
		bullet_pdu[1] = myplayer.id;
		var bullet_info = array(5);
		bullet_info[0] = myplayer.my_bullet.bull_id;
		bullet_info[1] = myplayer.my_bullet.direction;
		bullet_info[2] = myplayer.my_bullet.position;
		bullet_info[3] = myplayer.my_bullet.tipo;
		bullet_info[4] = myplayer.my_bullet.speed;
		bullet_pdu[2] = bullet_info;
		mySend(SERVER_IP, SERVER_PORT, bullet_pdu);
	}
	
	//I changed my position from the last one this function was called.
	//I must notificate the server of this change.
	if ( (old_pos != position) || (old_angle != angle) )
	{
		var pos_pdu = array(5);
		pos_pdu[0] = POSITION_PDU;
		pos_pdu[1] = position;
		pos_pdu[2] = angle;
		pos_pdu[3] = myplayer.id;
		pos_pdu[4] = myplayer.current_frame;
		mySend(SERVER_IP, SERVER_PORT, pos_pdu);
		
		for (var t = 0; t < len(arr_supplies); t++)
		{
			if ( (int(position.x) == arr_supplies[t].obj.getPosition().x) &&
				(int(position.z) == arr_supplies[t].obj.getPosition().z) )
			{
				var ammo_pdu = array(3);
				ammo_pdu[0] = COLLECTED_AMMO;
				ammo_pdu[1] = myplayer.id;
				ammo_pdu[2] = t;
				mySend(SERVER_IP, SERVER_PORT, ammo_pdu);
				myplayer.mystatus.recharge(arr_supplies[t].type);
				adel(arr_supplies, t);
			}
		}
	}
	
	//I changed my weapon from the last time this function was called.
	//I must notificate the server of this change.
	if (old_weapon != myplayer.curr_weapon)
	{
		old_weapon = myplayer.curr_weapon;
		
		var weapon_pdu = array(3);
		weapon_pdu[0] = CHANGE_WEAPON;
		weapon_pdu[1] = myplayer.id;
		weapon_pdu[2] = myplayer.curr_weapon;
		mySend(SERVER_IP, SERVER_PORT, weapon_pdu);
	}
	
	old_pos = position;
	old_angle = angle;
	
	myplayer.mystatus.update(myplayer.position, myplayer.view, myplayer.curr_weapon, myplayer.mystatus.myWeapon.n_bullets_rg);	
	mylevel.draw();
	
	//If I move my camera to first person, I'll hide the character and draw only the weapon.
	if (myplayer.view == 0)
	{
		myplayer.lower_obj.hide();
		myplayer.upper_obj.hide();
	}
	//If I go bakc to third person I will draw by avatar again.
	else
	{
		myplayer.lower_obj.unhide();
		myplayer.upper_obj.unhide();
	}
	
	myplayer.draw();
	var f = len(players);
	
	foreach (var t in players)
		t.draw_enemy();
		
	foreach (var t in arr_supplies)
		t.draw_box();

	foreach (var t in enemy_bullets)
	{
		t[1].position += t[1].direction * t[1].speed;
		t[1].obj.setPosition(t[1].position);
		t[1].draw_bullet();
		var p = myplayer.cylinder.iscollidingbbox(t[1].obj);
		if (p == true)
		{
			var player_bullet = array(3);
			player_bullet[0] = BULLET_ON_PERSON;
			player_bullet[1] = t[0];
			player_bullet[2] = t[1].bull_id;
			mySend(SERVER_IP, SERVER_PORT, player_bullet);	
			var hit = myplayer.mystatus.bullet_damage(t[1].tipo);
			if (hit == true)
			{
				var dead_pdu = array(3);
				dead_pdu[0] = DEAD_PLAYER;
				dead_pdu[1] = myplayer.id; //giocatore morto
				dead_pdu[2] = t[0];
				mySend(SERVER_IP, SERVER_PORT, dead_pdu);
				
				var k_score = array(2);
				k_score[0] = KILL_SCORE;
				k_score[1] = t[0];
				mySend(SERVER_IP, SERVER_PORT, k_score);
				
				OnExit();
				window.Quit();
			}
		}
	}
		
	SceneEnd();
}

function DownloadReady(RequestID)
{
	// TODO
}

/****************************************************
 *                    |OnTimer|                     *
 *--------------------------------------------------*
 * Called every 1ms, checks weather there are       *
 * messages sent by clients and acts accordingly:   *
 *                                                  *
 * NEW_PLAYER: a new player has entered the arena,  *
 * therefore i must add him to the structure        *
 * containing all of them, and then create an enemy *
 * object to draw on the map.                       *
 *                                                  *
 * EXIT_NOTIFICATION: a player has left the arena,  *
 * therefore we must find him in the structure and  *
 * remove him.                                      *
 *                                                  *
 * OLD_PLAYERS: sento to me from the server when I  *
 * join a game. It is a list of all the current     *
 * players in the arena, letting me start with them.*
 *                                                  *
 * POSITION_PDU: a PDU notifying me that a certain  *
 * player has moved his position or his angle. I    *
 * therefore set his position, his rotation and his *
 * current frame to correctly draw him.             *
 *                                                  *
 * CHANGE_WEAPON: notifyes me that a player changed *
 * his weapon, in order to correctly draw him.      *
 *                                                  *
 * NEW_AMMO: a new box containing life or bullets   *
 * has entered the arena. It can be collected.      *
 *                                                  *
 * OLD_AMMO: sent by the server as soon as i log on *
 * a game. It contains the existing boxes that are  *
 * currently on the ground, that spawned before I   *
 * logged on the arena.                             *
 *                                                  *
 * COLLECTED_AMMO: a player has collected an ammo,  *
 * therefore I must delete it from the structure.   *
 *                                                  *
 * SHOT_PDU: A player has shot a bullet. I must add *
 * it to the enemy_bullets array containing the id  *
 * of its player and the bullet itself, so I know   *
 * which one must be deleted.                       *
 *                                                  *
 * BULLET_ON_WALL: a bullet from another player has *
 * hit a wall. I must delete the corresponding      *
 * element from the enemy_bullets array structure.  *
 ****************************************************/

function OnTimer()
{
	var received_data = NetVarReceiveFrom(net_channel, &address1);
	if (received_data != null)
	{
		var enemyBull;
		var placeholder;
		var p;
		switch (received_data[0])
		{
			case NEW_PLAYER :
				myenemy = enemy();
				myenemy.init(received_data[2], received_data[3], received_data[1]);
				aadd(players, myenemy);
				break;
			
	 		case EXIT_NOTIFICATION :
	 			for (var t = 0; t < len(players); t++)
					if (players[t].id == received_data[1])
						adel(players, t);
				for (var t = 0; t < len(score_array); t++)
				{
					if (score_array[t][0] == received_data[1])
						adel(score_array, t);
				}
				break;
				
			case OLD_PLAYERS :
				arr = received_data[1];
				foreach (var t in received_data[1])
				{
					myenemy = enemy();
					myenemy.init(t[2], t[3], t[1]);
					myenemy.switch_weapon(t[4]);
					aadd(players, myenemy);				
				}
				break;

			case POSITION_PDU :
				for (var k = 0; k < len(players); k++)
				{
					if (received_data[3] == players[k].id)
					{
						players[k].obj.setPosition(received_data[1]);
						players[k].obj.setRotation(received_data[2]*180/pi, 0, 1, 0);
						players[k].current_frame = received_data[4];
					}
				}
				break;

			case CHANGE_WEAPON :
				for (var k = 0; k < len(players); k++)
					if (received_data[1] == players[k].id)
						players[k].switch_weapon(received_data[2]);
				break;
				
			case NEW_AMMO :
				if (len(arr_supplies) >= 3)
					adel(arr_supplies, 0);
				mybox = mysupply.create_supply(received_data[1][0], received_data[1][1]);
				if (mybox != null)
					aadd(arr_supplies, mybox);
				break;
				
			case OLD_AMMO :
				for (var k = 0; k < len(received_data[1]); k++)
				{
					mybox = mysupply.create_supply(received_data[1][k][0], received_data[1][k][0]);
					if (mybox != null)
						aadd(arr_supplies, mybox);
				}
				break;
				
			case COLLECTED_AMMO :
				adel(arr_supplies, received_data[2]);
				break;
				
			case SHOT_PDU :
				placeholder = array(2);
				placeholder[0] = received_data[1];
				enemyBull = bullet();
				enemyBull.init(received_data[2][2] - [0, 1, 0], received_data[2][1], level_matrix, 
					received_data[2][3],  received_data[2][0]);
				placeholder[1] = enemyBull;
				aadd(enemy_bullets, placeholder);
				break;
				
			case BULLET_ON_WALL :
				for(var k = 0; k < len(enemy_bullets); k++)
				{
					if ( (enemy_bullets[k][0] == received_data[1]) 
						&& (enemy_bullets[k][1].bull_id == received_data[2]) )
						adel(enemy_bullets, k);
				}
				break;
				
			case DEAD_PLAYER :
				for (var t = 0; t < len(players); t++)
					if (players[t].id == received_data[1])
							adel(players, t);
				for (var t = 0; t < len(score_array); t++)
				{
					if (score_array[t][0] == received_data[1])
						adel(score_array, t);
				}
				break;
				
			case BULLET_ON_PERSON :
				if (received_data[1] != myplayer.id)
				{
				for(var k = 0; k < len(enemy_bullets); k++)
					{
						if ( (enemy_bullets[k][0] == received_data[1]) 
							&& (enemy_bullets[k][1].bull_id == received_data[2]) )
							adel(enemy_bullets, k);
					}
				}
				else
					for(var k = 0; k < len(myplayer.bullet_array); k++)
					{
						if ( myplayer.bullet_array[k].bull_id == received_data[2] ) 
							
							adel(myplayer.bullet_array, k);
					}
				break;
			
			case OLD_SCORES : 
				score_array = received_data[1];
				break;
				
			case NEW_SCORE :
				p = array(2);
				p[0] = received_data[1];
				p[1] = received_data[2];
				aadd(score_array, p);
				break;
				
			case KILL_SCORE :
				for (var t = 0; t < len(score_array); t++)
					if (score_array[t][0] == received_data[1])
						score_array[t][1] += 1;
				break;
				
			default :
		}
	}
}


function OnEvent(eventID, wparam, lparam){
	// TODO: put your events handling code here
}

function OnError(){
	// TODO: put your errors handling code here
}

// Camera manager (using mouse)
function CameraMoveMouse()
{
	CameraMoveMouse_MBL_LC();
}